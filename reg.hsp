; ふじつぼ Automatic-Fujitsubo
; (C) 2021 inucat. Under GNU GPL.
#packopt name "Fujitsubo"
; #packopt manifest "fujitsubo.exe.manifest"
; #pack "fujitsubo.exe.manifest"
#include "hspext.as"
#include "hspinet.as"
#include "lang.hsp"
#cmpopt	varinit 1	; 未初期化の変数参照をエラーにする

#const N 7				; 設定可能なフォルダは7個
#const N_UP 13			; %UserProfile% の13個のフォルダ
#const CHK_W 128		; チェックボックス・ボタンの幅

APP_TITLE=i18n("appTitle") : title APP_TITLE
APP_VER="v1.0.0"

; %UserProfile% reg keys
upRoot = "HKLM\\SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Explorer\\FolderDescriptions\\"
upRoot32 = "HKLM\\SOFTWARE\\Wow6432Node\\Microsoft\\Windows\\CurrentVersion\\Explorer\\FolderDescriptions\\"
upPic =	"{0ddd015d-b06c-45d5-8c4c-f59713854639}\\PropertyBag"	; Picture
upVid =	"{35286a68-3c57-41a1-bbb1-0eae73d76c95}\\PropertyBag"	; Video
upDwn =	"{7d83ee9b-2244-4e70-b1f5-5393042af1e4}\\PropertyBag"	; Download
upMus =	"{a0c69a99-21c8-4671-8703-7934162fcf1d}\\PropertyBag"	; Music
upDoc =	"{f42ee2d3-909f-4907-8871-4c22fc0bf756}\\PropertyBag"	; Documents
up3do =	"{31C0DD25-9439-4F12-BF41-7FF4EDA38722}\\PropertyBag"	; 3D Objects
upCon = "{56784854-C6CB-462B-8169-88E350ACB882}\\PropertyBag"	; Contacts
upLnk = "{bfb9d5e0-c6a9-404c-b2b2-ae6db6af4968}\\PropertyBag"	; Links
upOne = "{A52BBA46-E9E1-435f-B3D9-28DAA648C0F6}\\PropertyBag"	; OneDrive
upGam = "{4C5C32FF-BB9D-43b0-B5B4-2D72E54EAAA4}\\PropertyBag"	; Saved Games
upSea = "{7d1d3a04-debb-4115-95cf-2f29da2920da}\\PropertyBag"	; Searches
upFa1 = "{1777F761-68AD-4D8A-87BD-30B759FA33DD}\\PropertyBag"	; Favorites 1st key
upFa2 = "{7CFBEFBC-DE1F-45AA-B843-A542AC536CC9}\\PropertyBag"	; Favorites 2nd key
upDe1 =	"{B4BFCC3A-DB2C-424C-B029-7FE99A87C641}\\PropertyBag"	; Desktop 1st key
upDe2 =	"{754AC886-DF64-4CBA-86B5-F7FBF4FBCEF5}\\PropertyBag"	; Desktop 2nd key

; "This PC" reg keys
root64="HKEY_LOCAL_MACHINE\\SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Explorer\\MyComputer\\NameSpace\\"
root32="HKEY_LOCAL_MACHINE\\SOFTWARE\\Wow6432Node\\Microsoft\\Windows\\CurrentVersion\\Explorer\\MyComputer\\NameSpace\\"
d3oKey="{0DB7E03F-FC29-4DC6-9020-FF41B59E513A}"
desKey="{B4BFCC3A-DB2C-424C-B029-7FE99A87C641}"
docKey1="{A8CDFF1C-4878-43be-B5FD-F8091C1C60D0}"
docKey2="{d3162b92-9365-467a-956b-92703aca08af}"
dwnKey1="{374DE290-123F-4565-9164-39C4925E467B}"
dwnKey2="{088e3905-0323-4b02-9826-5d99428e115f}"
musKey1="{1CF1260C-4DD0-4ebb-811F-33C572699FDE}"
musKey2="{3dfdf296-dbec-4fb4-81d1-6a3438bcf4de}"
picKey1="{3ADD1653-EB32-4cb0-BBD7-DFA0ABB5ACCA}"
picKey2="{24ad3ad4-a569-4530-98e1-ab02f9417aa8}"
vidKey1="{A0953C92-50DC-43bf-BE83-3742FED03C9C}"
vidKey2="{f86fa3ab-70d2-4fc7-9c99-fcbf05467f3a}"

dim isVisible,N
dim keys,N+5 : keys=d3oKey, desKey, docKey1, dwnKey1, musKey1, picKey1, vidKey1, docKey2, dwnKey2, musKey2, picKey2, vidKey2
dim keyLabels,N : keyLabels=i18n("3D Object"),i18n("Desktop"),i18n("Documents"),i18n("Downloads"),i18n("Music"),i18n("Pictures"),i18n("Video")
dim upVisible,N_UP
dim upKeys,N_UP : upKeys=up3do, upDoc, upDwn, upMus, upPic, upVid, upCon, upLnk, upOne, upGam, upSea, upFa1, upDe1, upFa2, upDe2 
dim upLabels,N_UP : upLabels=i18n("3D Object"),i18n("Documents"),i18n("Downloads"),i18n("Music"),i18n("Pictures"),i18n("Video"),i18n("Contacts"),i18n("Links"),i18n("OneDrive"),i18n("Saved Games"),i18n("Searches"),i18n("Favorites"),i18n("Desktop")

; Detect Windows OS bit (x86 or not)
osBit=""
getenv osBit, "PROCESSOR_ARCHITECTURE"
if osBit == "x86" {;   osBit=="x86" --> is WOW64 or true 32bit Win?
	getenv osBit, "PROCESSOR_ARCHITEW6432" ;   32bit --> osBit="x86"
} logmes osBit

; Check Admin Privilege
#uselib "shell32.dll"
#cfunc IsUserAnAdmin  "IsUserAnAdmin"
if IsUserAnAdmin(){
	logmes "Good."
}else{
	dialog i18n("adminDialog"),1,APP_TITLE
	end
}

*load_keys
	screen ,640,540 : cls 1 : mes i18n("loadReg") : logmes "\n===LOADING KEYS==="

	sdim buf,32000 : sdim ln,4096
	pipeexec buf,"reg query " + root64 + " /reg:64"
	if stat : dialog i18n("critical"),1 : end
	repeat
		pipeget ln : if stat == 0 : break
	loop
	logmes buf

	foreach keyLabels ; Set isVisible.cnt to 1 if each key is under root, otherwise 0
		if instr( buf , , keys.cnt ) >= 0 : isVisible.cnt=1 : else : isVisible.cnt=0
	loop

	; Load %USERPROFILE% status
	foreach upLabels ; Set isVisible.cnt to 1 if each key is under root, otherwise 0
		buf="" ; Clear pipeline buffer 
		pipeexec buf,"reg query " +upRoot+upKeys.cnt+ " /s /reg:64" : if stat : dialog i18n("critical"),1 : end
		repeat
			pipeget ln : if stat == 0 : break
		loop
		logmes buf
		if instr( buf , , "Hide" ) < 0 : upVisible.cnt=1 : else : upVisible.cnt=0
	loop

*fresh_gui
	cls 1
	objsize CHK_W
	mes i18n("PC") ; This PC
	button i18n("Hideall"), *all_hide
	button i18n("Showall"), *all_show
	foreach isVisible
		chkbox keyLabels.cnt, isVisible.cnt
	loop
	
	pos CHK_W,0
	mes i18n("user's") ; %UserProfile%
	button i18n("Hideall"), *all_hide_up
	button i18n("Showall"), *all_show_up
	foreach upVisible
		chkbox upLabels.cnt, upVisible.cnt
	loop

	pos CHK_W*2,0
	mes : button i18n("Reload"), *load_keys : mes
	mes : mes : mes : button i18n("Apply"), *apply
	mes : button i18n("About"), *about

	pos 0,400 : mes i18n("alsoHide")
	stop

*apply
	cls 1 : mes "適用中...お待ちください" : logmes "===APPLYING==="; Applying message
	foreach keyLabels
		command=""
		if isVisible.cnt == 0 : command = "DELETE " : else : command = "ADD "
		
		; logmes "------"
		sdim buf,32000 : sdim ln,4096
		pipeexec buf,"REG " + command + root64 + keys.cnt + " /f /reg:64"
		if stat : dialog i18n("critical") : goto *fresh_gui
		repeat
			pipeget ln : if stat==0 : break
		loop
		; logmes buf
		; WOW6432 以下の設定
		if osBit != "x86" {
			pipeexec buf,"REG " + command + root32 + keys.cnt + " /f /reg:64"
			if stat : dialog i18n("critical") : goto *fresh_gui
			repeat
				pipeget ln : if stat==0 : break
			loop
			; logmes buf
		}
		if cnt > 1 {
			pipeexec buf,"REG " + command + root64 + keys.(cnt+5) + " /f /reg:64"
			if stat : dialog i18n("critical") : goto *fresh_gui
			repeat
				pipeget ln : if stat==0 : break
			loop
			; logmes buf
			; WOW6432 以下の設定				
			if osBit != "x86" {
				pipeexec buf,"REG " + command + root32 + keys.cnt + " /f /reg:64"
				if stat : dialog i18n("critical") : goto *fresh_gui
				repeat
					pipeget ln : if stat==0 : break
				loop
				; logmes buf
			}
		}
	loop

	; %UserProfile% application
	foreach upVisible
		if upVisible.cnt == 1 : policy="Show" : else : policy="Hide"
		pipeexec buf,"REG ADD " + upRoot+upKeys.cnt + " /v ThisPCPolicy /d " +policy+ " /f /reg:64"
		if stat : dialog i18n("critical") : goto *fresh_gui
		repeat
			pipeget ln : if stat==0 : break
		loop
		if osBit != "x86" {
			pipeexec buf,"REG ADD " + upRoot32+upKeys.cnt + " /v ThisPCPolicy /d " +policy+ " /f /reg:64"
			if stat : dialog i18n("critical") : goto *fresh_gui
			repeat
				pipeget ln : if stat==0 : break
			loop
		}
		if cnt > 10 {
			pipeexec buf,"REG ADD " + upRoot+upKeys.(cnt+2) + " /v ThisPCPolicy /d " +policy+ " /f /reg:64"
			if stat : dialog i18n("critical") : goto *fresh_gui
			repeat
				pipeget ln : if stat==0 : break
			loop
			if osBit != "x86" {
				pipeexec buf,"REG ADD " + upRoot32+upKeys.(cnt+2) + " /v ThisPCPolicy /d " +policy+ " /f /reg:64"
				if stat : dialog i18n("critical") : goto *fresh_gui
				repeat
					pipeget ln : if stat==0 : break
				loop
			}
		}
		; buf="" : ln="" ; Clear pipeline buffer 
	loop
	dialog i18n("finish"),,APP_TITLE
	goto *fresh_gui

*all_hide
	foreach isVisible
		isVisible.cnt=0
	loop
	goto *fresh_gui

*all_show
	foreach isVisible
		isVisible.cnt=1
	loop
	goto *fresh_gui

*all_show_up
	foreach upVisible
		upVisible.cnt=1
	loop
	goto *fresh_gui

*all_hide_up
	foreach upVisible
		upVisible.cnt=0
	loop
	goto *fresh_gui

*about
	dialog APP_TITLE +"\n"+ APP_VER +"\n(C) 2021 inucat\n\n"+i18n("detail"),,APP_TITLE
	stop