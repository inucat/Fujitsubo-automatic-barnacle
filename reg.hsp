; ふじつぼ Automatic-Fujitsubo
; (C) 2021 inucat. Under GNU GPL.
APP_TITLE="ふじつぼ Fujitsubo"
title APP_TITLE
APP_VER="0.1.0"

#packopt	name	"Fujitsubo"
#include	"hspext.as"
#include	"hspinet.as"
#cmpopt		varinit 1	; 未初期化の変数参照をエラーにする

#const N 7				; 設定可能なフォルダは7個
#const CHK_W 128		; チェックボックス・ボタンの幅

screen ,280,240

ROOT = "HKLM\\SOFTWARE\\Microsoft\\Windows\\CurrentVersion\\Explorer\\FolderDescriptions"
P_KEY =		"{0ddd015d-b06c-45d5-8c4c-f59713854639}\\PropertyBag"	; ピクチャ
V_KEY =		"{35286a68-3c57-41a1-bbb1-0eae73d76c95}\\PropertyBag"	; ビデオ
DL_KEY =	"{7d83ee9b-2244-4e70-b1f5-5393042af1e4}\\PropertyBag"	; ダウンロード
M_KEY =		"{a0c69a99-21c8-4671-8703-7934162fcf1d}\\PropertyBag"	; ミュージック
DOC_KEY =	"{f42ee2d3-909f-4907-8871-4c22fc0bf756}\\PropertyBag"	; ドキュメント
DES_KEY =	"{B4BFCC3A-DB2C-424C-B029-7FE99A87C641}\\PropertyBag"	; デスクトップ
D3_KEY =	"{31C0DD25-9439-4F12-BF41-7FF4EDA38722}\\PropertyBag"	; 3D

dim isVisible,N
dim keyList,N
keyList=P_KEY,V_KEY,DL_KEY,M_KEY,DOC_KEY,DES_KEY,D3_KEY
dim keyLabel,N
keyLabel="ピクチャ","ビデオ","ダウンロード","ミュージック","ドキュメント","デスクトップ","3D オブジェクト"

*load_keys
	cls 1
	mes "レジストリの読み取り中..."
	logmes "\n===LOADING KEYS==="
	foreach keyList
		logmes "------"

		sdim buf,32000
		sdim ln,4096
		pipeexec buf,"reg query "+ROOT+"\\"+keyList.cnt+" /reg:64"
		if stat : dialog "予期しないレジストリ・エラーが発生しました。" : end
		repeat
			pipeget ln
			if stat == 0 : break
			wait 1
		loop
		logmes buf
		if instr( buf , , "Hide" ) < 0 : isVisible.cnt=1 : else : isVisible.cnt=0
	loop

*fresh_gui
	cls 1
	objsize CHK_W
	foreach keyList
		chkbox keyLabel.cnt,isVisible.cnt
	loop
	mes "\nヒント：管理者権限が無いと\n正しく適用されません。"

	pos CHK_W,0
	button "再読み込み", *load_keys
	button "すべて隠す", *all_hide
	button "すべて現す", *all_show
	mes
	button "変更を適用", *apply
	mes
	button "このアプリについて", *about
	stop

*apply
	logmes "\n===APPLYING==="
	foreach keyList
		policy=""
		; errlv="OKASHIMOCHI"
		if isVisible.cnt == 0 : policy = "Hide" : else : policy = "Show"
		
		logmes "------"
		sdim buf,32000
		sdim ln,4096
		pipeexec buf,"reg add "+ROOT+"\\"+keyList.cnt+" /v ThisPCPolicy /d "+ policy +" /f /reg:64"
		if stat : dialog "予期しないレジストリ・エラーが発生しました。" : goto *fresh_gui
		repeat
			pipeget ln
			if stat==0 : break
			wait 1
		loop
		; getenv errlv,"errorlevel"
		; logmes errlv
		logmes buf
		if instr( buf , , "Hide" ) < 0 : isVisible.cnt=1 : else : isVisible.cnt=0
	loop
	dialog "変更を適用しました。\n正しく適用されていない場合は、\nこのアプリを管理者として起動し\n再度適用してください。",,APP_TITLE
	stop

*all_hide
	foreach isVisible
		isVisible.cnt=0
	loop
	goto *fresh_gui

*all_show
	foreach isVisible
		isVisible.cnt=1
	loop
	goto *fresh_gui

*about
	dialog APP_TITLE + "\nVER. "+ APP_VER +"\n(C) 2021 inucat\n\n詳細・使い方はREADMEを参照。",,APP_TITLE
	stop